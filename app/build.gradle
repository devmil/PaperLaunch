apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'

android {
    defaultConfig {
        applicationId "de.devmil.paperlaunch"
        minSdkVersion 21
        compileSdkVersion 34
        targetSdkVersion 34
        versionCode 17
        versionName "1.2.0"
        archivesBaseName = "paperlaunch-v${defaultConfig.versionName}-${buildTime()}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    signingConfigs {
        release {
            def propsFile = file('../Key/keystore.properties')
            if (propsFile.exists()) {
                println "Keystore properties found! Building signed..."
                Properties keyProps = new Properties()
                keyProps.load(new FileInputStream(propsFile))
                storeFile keyProps["store"] != null ? file(keyProps["store"]) : null
                keyAlias keyProps["alias"] != null ? keyProps["alias"] : null
                storePassword keyProps["storePass"] != null ? keyProps["storePass"] : null
                keyPassword keyProps["pass"] != null ? keyProps["pass"] : null
            } else {
                println "No Keystore properties found! Building unsigned..."
            }
        }
    }
    buildTypes {
        release {
            def propsFile = file('../Key/keystore.properties')
            if(propsFile.exists()) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lint {
        abortOnError false
    }
    namespace 'de.devmil.paperlaunch'

}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation "com.android.support:recyclerview-v7:$android_support_version"
    implementation "com.android.support:design:$android_support_version"
    implementation "com.android.support:palette-v7:$android_support_version"
    implementation 'io.reactivex:rxandroid:1.1.0'
    implementation 'io.reactivex:rxjava:1.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.android.support.constraint:constraint-layout:2.0.4'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.0.0'
}
repositories {
    mavenCentral()
}

static def buildTime() {
    return new Date().format("yyyyMMdd")
}
